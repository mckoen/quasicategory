import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Quasicategory.KInjective.Induction
import Quasicategory.KInjective.StableUnderRetract

universe w v u

namespace CategoryTheory

open Category Limits

variable {C : Type u} [Category.{v} C]

namespace IsPushout

variable {X Y Z T : C} (t : X ‚ü∂ Y) (l : X ‚ü∂ Z) (r : Y ‚ü∂ T) (b : Z ‚ü∂ T)
  (h : IsPushout t l r b)

lemma hasLiftingProperty (t : X ‚ü∂ Y) (l : X ‚ü∂ Z) (r : Y ‚ü∂ T) (b : Z ‚ü∂ T)
    (h : IsPushout t l r b)
    {A B : C} (p : A ‚ü∂ B) [HasLiftingProperty l p] :
    HasLiftingProperty r p where
  sq_hasLift {f g} sq := by
    have sq' : CommSq (t ‚â´ f) l p (b ‚â´ g) :=
      ‚ü®by rw [assoc, sq.w, h.w_assoc]‚ü©
    exact ‚ü®‚ü®{
      l := PushoutCocone.IsColimit.desc h.isColimit f sq'.lift (by simp)
      fac_left := PushoutCocone.IsColimit.inl_desc h.isColimit _ _ _
      fac_right := by
        apply PushoutCocone.IsColimit.hom_ext h.isColimit
        ¬∑ rw [PushoutCocone.IsColimit.inl_desc_assoc, cocone_inl, sq.w]
        ¬∑ rw [PushoutCocone.IsColimit.inr_desc_assoc, cocone_inr, sq'.fac_right] }‚ü©‚ü©

end IsPushout

/-
instance hasLiftingProperty_pushout_inl {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) [HasPushout f g] {A B : C} (p : A ‚ü∂ B)
    [HasLiftingProperty g p] : HasLiftingProperty (pushout.inl f g : Y ‚ü∂ pushout f g) p :=
  (IsPushout.of_hasPushout f g).hasLiftingProperty p

instance hasLiftingProperty_pushout_inr {X Y Z : C} (f : X ‚ü∂ Y) (g : X ‚ü∂ Z) [HasPushout f g] {A B : C} (p : A ‚ü∂ B)
    [HasLiftingProperty f p] : HasLiftingProperty (pushout.inr f g: Z ‚ü∂ pushout f g) p :=
  (IsPushout.of_hasPushout f g).flip.hasLiftingProperty p
-/

instance hasLiftingProperty_limits_map {I : Type*} {A B : I ‚Üí C} (f : ‚àÄ i, A i ‚ü∂ B i)
    [HasCoproduct A] [HasCoproduct B] {X Y : C} (p : X ‚ü∂ Y) [‚àÄ i, HasLiftingProperty (f i) p] :
    HasLiftingProperty (Limits.Sigma.map f) p where
  sq_hasLift {t b} sq := by
    have sq' : ‚àÄ i, CommSq (Sigma.Œπ A i ‚â´ t) (f i) p (Sigma.Œπ B i ‚â´ b) := fun i => ‚ü®by simp [sq.w]‚ü©
    exact ‚ü®‚ü®{
        l := Sigma.desc (fun i => (sq' i).lift)
        fac_left := by aesop_cat
        fac_right := by aesop_cat }‚ü©‚ü©

namespace MorphismProperty

section

variable {I : Type w} {A B : I ‚Üí C} (f : ‚àÄ i, A i ‚ü∂ B i)

inductive ofHoms : MorphismProperty C
  | mk (i : I) : ofHoms (f i)

lemma ofHoms.mk_mem (i : I) : (ofHoms f) (f i) := ofHoms.mk i

end

variable (W : MorphismProperty C)

def llpWith : MorphismProperty C := fun _ _ i =>
  ‚àÄ ‚¶ÉX Y : C‚¶Ñ (p : X ‚ü∂ Y) (_ : W p), HasLiftingProperty i p

def rlpWith : MorphismProperty C := fun _ _ p =>
  ‚àÄ ‚¶ÉA B : C‚¶Ñ (i : A ‚ü∂ B) (_ : W i), HasLiftingProperty i p

instance : W.llpWith.ContainsIdentities where
  id_mem _ _ _ _ _ := inferInstance

instance : W.llpWith.IsMultiplicative where
  comp_mem := by
    intro _ _ _ i j hi hj _ _ p hp
    have := hi p hp
    have := hj p hp
    infer_instance

instance : W.rlpWith.ContainsIdentities where
  id_mem _ _ _ _ _ := inferInstance

instance : W.rlpWith.IsMultiplicative where
  comp_mem := by
    intro _ _ _ p q hp hq _ _ i hi
    have := hp i hi
    have := hq i hi
    infer_instance

lemma le_llpWith_rlpWith : W ‚â§ W.llpWith.rlpWith := by
  intro X Y p hp A B i hi
  have := hi p hp
  infer_instance

lemma le_rlpWith_llpWith : W ‚â§ W.rlpWith.llpWith := by
  intro A B i hi X Y p hp
  have := hp i hi
  infer_instance

lemma llpWith_isStableUnderRetract : W.llpWith.IsStableUnderRetract where
  condition f g i p hip hg _ _ œÄ hœÄ := hasLeftLiftingProperty_of_retract f g i p hip œÄ (hg œÄ hœÄ)

lemma rlpWith_isStableUnderRetract : W.rlpWith.IsStableUnderRetract where
  condition f g i p hip hg _ _ Œπ hŒπ := hasRightLiftingProperty_of_retract f g i p hip Œπ (hg Œπ hŒπ)

/-
lemma llpWith_respectsIso : W.llpWith.RespectsIso where
  precomp e f hf X Y p hp := by
    have := hf p hp
    infer_instance
  postcomp e f hf X Y p hp := by
    have := hf p hp
    infer_instance

lemma rlpWith_respectsIso : W.rlpWith.RespectsIso where
  precomp e f hf A B i hi := by
    have := hf i hi
    infer_instance
  postcomp e f hf A B i hi := by
    have := hf i hi
    infer_instance
-/

namespace IsStableUnderTransfiniteCompositionOfShapeLlpWith

variable {W}
variable {Œ≤ : Type*} [LinearOrder Œ≤] [IsWellOrder Œ≤ (¬∑ < ¬∑)] [OrderBot Œ≤]
  {F : Œ≤ ‚•§ C} [F.WellOrderContinuous]
  (hF : ‚àÄ (a : Œ≤) (_ : a < wellOrderSucc a), W.llpWith (F.toPrefunctor.map (homOfLE (self_le_wellOrderSucc a))))
  {c : Cocone F} (hc : IsColimit c)

section

variable {X Y : C} {p : X ‚ü∂ Y} (hp : W p)
  {f : F.obj ‚ä• ‚ü∂ X} {g : c.pt ‚ü∂ Y} (sq : CommSq f (c.Œπ.app ‚ä•) p g)

lemma commSq (sq : CommSq f (c.Œπ.app ‚ä•) p g)
    (i : Œ≤) : CommSq f (F.map (homOfLE bot_le)) p (c.Œπ.app i ‚â´ g) where
  w := by rw [sq.w, Cocone.w_assoc]

variable (F) in
@[simps]
def system : Œ≤·µí·µñ ‚•§ Type _ where
  obj i := (commSq sq i.unop).LiftStruct
  map {i j} œÜ := fun s =>
    { l := F.map œÜ.unop ‚â´ s.l
      fac_left := by simpa only [‚Üê Functor.map_comp_assoc] using s.fac_left
      fac_right := by
        dsimp
        rw [assoc, s.fac_right, Cocone.w_assoc] }

section

variable {sq}
variable (j : Œ≤) (hj : j < wellOrderSucc j) (s : (system F sq).obj (Opposite.op j))

variable {j} in
lemma wellOrderInductionDataSucc_sq :
    CommSq s.l (F.map (homOfLE (self_le_wellOrderSucc j))) p
    (c.Œπ.app (wellOrderSucc j) ‚â´ g) where
  w := by simp [s.fac_right]

noncomputable def wellOrderInductionDataSucc :
    (system F sq).obj (Opposite.op (wellOrderSucc j)) :=
  letI := hF j hj p hp
  { l := (wellOrderInductionDataSucc_sq s).lift
    fac_left := by
      conv_rhs => rw [‚Üê s.fac_left, ‚Üê (wellOrderInductionDataSucc_sq s).fac_left]
      rw [‚Üê F.map_comp_assoc, homOfLE_comp]
    fac_right := by simp }

lemma wellOrderInductionData_map_succ :
    (system F sq).toPrefunctor.map (homOfLE (self_le_wellOrderSucc j)).op
      (wellOrderInductionDataSucc hF hp j hj s) = s := by
  letI := hF j hj p hp
  exact CommSq.LiftStruct.ext (wellOrderInductionDataSucc_sq s).fac_left

end

section

variable {sq}
variable {j : Œ≤} [IsWellOrderLimitElement j]
  (x : (((PrincipalSeg.ofElement (¬∑ < ¬∑) j).functorToOver ‚ãô Over.forget _).op ‚ãô system F sq).sections)

instance (Œ≤ : Type*) [LinearOrder Œ≤] (j : Œ≤) [IsWellOrderLimitElement j] :
    IsWellOrderLimitElement (PrincipalSeg.ofElement (¬∑ < ¬∑) j).top := by
  dsimp
  infer_instance

noncomputable def wellOrderInductionDataDesc : (system F sq).obj (Opposite.op j) where
  l := (F.isColimitOfWellOrderContinuous (PrincipalSeg.ofElement (¬∑ < ¬∑) j)).desc
    (Cocone.mk _
      { app := fun i => (x.1 (Opposite.op i)).l
        naturality := fun i i' œÜ => by
          dsimp
          rw [comp_id, ‚Üê x.2 œÜ.op]
          rfl })
  fac_left := by
    have hj := IsWellOrderLimitElement.bot_lt j
    have h : F.map (ùüô _) ‚â´ _ = _ := (x.1 (Opposite.op ‚ü®‚ä•, hj‚ü©)).fac_left
    rw [F.map_id, id_comp] at h
    erw [(F.isColimitOfWellOrderContinuous (PrincipalSeg.ofElement (¬∑ < ¬∑) j)).fac _ ‚ü®‚ä•, hj‚ü©]
    exact h
  fac_right := (F.isColimitOfWellOrderContinuous (PrincipalSeg.ofElement (¬∑ < ¬∑) j)).hom_ext (fun i => by
    rw [IsColimit.fac_assoc]
    dsimp
    rw [Cocone.w_assoc, (x.1 (Opposite.op i)).fac_right]
    rfl)

lemma wellOrderInductionData_fac {i : Œ≤} (hi : i < j) :
    (system F sq).map (homOfLE hi.le).op (wellOrderInductionDataDesc x) = x.val ‚ü®i, hi‚ü© :=
  CommSq.LiftStruct.ext
    ((F.isColimitOfWellOrderContinuous (PrincipalSeg.ofElement (¬∑ < ¬∑) j)).fac _ ‚ü®i, hi‚ü©)

end

noncomputable def wellOrderInductionData : (system F sq).WellOrderInductionData where
  succ j hj s := wellOrderInductionDataSucc hF hp j hj s
  map_succ j hj s := wellOrderInductionData_map_succ hF hp j hj s
  desc _ _ x := wellOrderInductionDataDesc x
  fac _ _ _ hi x := wellOrderInductionData_fac x hi

variable (c) in
noncomputable def systemSection : (system F sq).sections :=
  (wellOrderInductionData hF hp sq).sectionsMk
    { l := f
      fac_left := by
        change F.map (ùüô _) ‚â´ f = f
        simp
      fac_right := sq.w }

noncomputable def liftStruct : sq.LiftStruct where
  l := hc.desc (Cocone.mk _
    { app := fun i => ((systemSection hF c hp sq).1 (Opposite.op i)).l
      naturality := fun i j œÜ => by
        dsimp
        rw [comp_id, ‚Üê (systemSection hF c hp sq).2 œÜ.op]
        rfl })
  fac_left := by
    have h := ((systemSection hF c hp sq).1 (Opposite.op ‚ä•)).fac_left
    simp only [IsColimit.fac, ‚Üê h]
    convert (id_comp _).symm
    apply F.map_id
  fac_right := hc.hom_ext (fun i => by
    dsimp
    simpa only [IsColimit.fac_assoc] using ((systemSection hF c hp sq).1 (Opposite.op i)).fac_right)

end

lemma hasLiftingProperty
    (hF : ‚àÄ (a : Œ≤) (_ : a < wellOrderSucc a), W.llpWith (F.toPrefunctor.map (homOfLE (self_le_wellOrderSucc a))))
    (hc : IsColimit c)
    {X Y : C} {p : X ‚ü∂ Y} (hp : W p) : HasLiftingProperty (c.Œπ.app ‚ä•) p where
  sq_hasLift sq := ‚ü®‚ü®liftStruct hF hc hp sq‚ü©‚ü©

lemma llpWith_Œπ_app
    (hF : ‚àÄ (a : Œ≤) (_ : a < wellOrderSucc a), W.llpWith (F.toPrefunctor.map (homOfLE (self_le_wellOrderSucc a))))
    (hc : IsColimit c) :
    W.llpWith (c.Œπ.app ‚ä•) :=
  fun _ _ _ hp => hasLiftingProperty hF hc hp

end IsStableUnderTransfiniteCompositionOfShapeLlpWith

instance (Œ≤ : Type*) [LinearOrder Œ≤] [IsWellOrder Œ≤ (¬∑ < ¬∑)] [OrderBot Œ≤] :
  W.llpWith.IsStableUnderTransfiniteCompositionOfShape Œ≤ where
    condition _ _ hF _ hc :=
      IsStableUnderTransfiniteCompositionOfShapeLlpWith.llpWith_Œπ_app hF hc

instance llpWith_comp : W.llpWith.IsStableUnderTransfiniteComposition where

end MorphismProperty

end CategoryTheory
