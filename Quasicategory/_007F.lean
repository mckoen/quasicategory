import Quasicategory._007F_1
import Quasicategory._007F.Tau
import Quasicategory._007F.Sigma

/-!

The second half of the proof of `007F`, which is much more technical.

-/

universe w

namespace SSet

open CategoryTheory MorphismProperty Simplicial SSet PushoutProduct MonoidalCategory Subcomplex

variable {n : ‚Ñï}

noncomputable
def image_arrow_iso_of_mono {X Y : SSet} (f : X ‚ü∂ Y) [Mono f] (A : Subcomplex X) :
    Arrow.mk (Subcomplex.homOfLE (image_le_range A f)) ‚âÖ Arrow.mk A.Œπ := by
  let h := IsIso.out (f := (toRangeSubcomplex (A.Œπ ‚â´ f)))
  let ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© := h.choose_spec
  refine Arrow.isoMk ((isoOfEq (image_eq_range A f)) ‚â™‚â´ (asIso (toRangeSubcomplex (A.Œπ ‚â´ f))).symm)
    (asIso (toRangeSubcomplex f)).symm ?_
  simp
  change _ ‚â´ h.choose ‚â´ _ ‚â´ _ = _
  ext n ‚ü®y, ‚ü®x, ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©‚ü©‚ü©
  have := congr_fun (congr_app h‚ÇÅ n) ‚ü®x, hx‚ÇÅ‚ü©
  simp [Subcomplex.homOfLE, Subpresheaf.homOfLe, toRangeSubcomplex,
    Subpresheaf.toRange, Subpresheaf.lift, ‚Üê hx‚ÇÇ] at this ‚ä¢
  aesop

instance {X Y Z : SSet} : Subsingleton ((Y ‚äó (‚ä• : X.Subcomplex).toSSet) ‚ü∂ Z) where
  allEq f g := by
    ext _ ‚ü®_, ‚ü®x, hx‚ü©‚ü©
    simp at hx

instance {X Y Z : SSet} : Inhabited ((Y ‚äó (‚ä• : X.Subcomplex).toSSet) ‚ü∂ Z) where
  default :=
    { app _ := fun ‚ü®_, ‚ü®_, hx‚ü©‚ü© ‚Ü¶ by simp at hx
      naturality _ _ _ := by
        ext ‚ü®_, ‚ü®_, hx‚ü©‚ü©
        simp at hx }

instance {X Y Z : SSet} : Unique ((Y ‚äó (‚ä• : X.Subcomplex).toSSet) ‚ü∂ Z)  where
  uniq _ := Subsingleton.elim _ _

noncomputable
def Subcomplex.tensorBotIsInitial {X Y : SSet} : Limits.IsInitial (Y ‚äó (‚ä• : X.Subcomplex).toSSet) :=
  Limits.IsInitial.ofUnique _

noncomputable
def pt_terminal_iso :
    Limits.pushout (Œõ[2, 1].Œπ ‚ñ∑ (‚ä• : Œî[0].Subcomplex).toSSet) (Œõ[2, 1].toSSet ‚óÅ ((‚ä• : Œî[0].Subcomplex)).Œπ) ‚âÖ
      Œõ[2, 1].toSSet ‚äó Œî[0] where
  hom := Limits.pushout.desc (Limits.IsInitial.to Subcomplex.tensorBotIsInitial _) (ùüô _) (by aesop_cat)
  inv := Limits.pushout.inr _ _
  hom_inv_id := by
    apply Limits.pushout.hom_ext
    all_goals aesop_cat

noncomputable
def zero_unionProd_arrowIso' :
    Arrow.mk (Œõ[2, 1].unionProd (‚ä• : Œî[0].Subcomplex)).Œπ ‚âÖ
      Arrow.mk (Œõ[2, 1].Œπ ‚ñ∑ Œî[0]) := by
  refine Arrow.isoMk ((IsPushout.isoPushout (Subcomplex.unionProd.isPushout _ _)) ‚â™‚â´ pt_terminal_iso) (Iso.refl _) ?_
  apply IsPushout.hom_ext (Subcomplex.unionProd.isPushout _ _)
  ¬∑ aesop_cat
  ¬∑ simp [pt_terminal_iso]

noncomputable
def zero_unionProd_arrowIso :
    Arrow.mk ((‚ä• : Œî[0].Subcomplex).unionProd Œõ[2, 1]).Œπ ‚âÖ
      Arrow.mk (Œõ[2, 1].Œπ) := by
  refine ?_ ‚â™‚â´ zero_unionProd_arrowIso' ‚â™‚â´ ?_
  ¬∑ exact Arrow.isoMk (Subcomplex.unionProd.symmIso _ _) (Œ≤_ _ _) rfl
  ¬∑ exact Arrow.isoMk (stdSimplex.rightUnitor _) (stdSimplex.rightUnitor _) rfl

section

variable {C : Type*} [Category C] (W : MorphismProperty C) [W.IsMultiplicative]

-- go from `‚ü®b, a‚ü© --> ‚ü®b, a'‚ü©` for `a ‚â§ a'`
lemma _root_.CategoryTheory.MorphismProperty.map_mem_of_sigma' {n : ‚Ñï} (F : (Œ£‚Çó (b : Fin (n + 1)), Fin b.succ) ‚•§ C)
    (hF : ‚àÄ (i : Œ£‚Çó (b : Fin (n + 1)), Fin b.succ), W (F.map (homOfLE (Sigma.Lex.le_succ i))))
    {b : Fin (n + 1)} (a a' : Fin b.succ) (h : a ‚â§ a') :
    W (F.map (homOfLE (show ‚ü®b, a‚ü© ‚â§ ‚ü®b, a'‚ü© by right; simpa))) := by
  obtain ‚ü®b, hb‚ü© := b
  obtain ‚ü®a, ha‚ü© := a
  obtain ‚ü®a', ha'‚ü© := a'
  induction a' with
  | zero =>
    simp only [Fin.le_iff_val_le_val, le_zero_iff] at h
    subst h
    simp only [Fin.val_succ, Fin.succ_mk, Fin.zero_eta, CategoryTheory.homOfLE_refl,
      CategoryTheory.Functor.map_id]
    apply id_mem
  | succ a' h' =>
  cases lt_or_eq_of_le h
  ¬∑ next h'' =>
    have one := h' (by omega) (by simp [Fin.lt_iff_val_lt_val, Fin.le_iff_val_le_val] at h'' ‚ä¢; omega)
    have two := hF ‚ü®‚ü®b, hb‚ü©, ‚ü®a', by omega‚ü©‚ü©
    have eq := Sigma.Lex.Fin.succ_eq_of_snd_lt_fst ‚ü®b, hb‚ü© ‚ü®a', by omega‚ü© (by simpa [Fin.lt_iff_val_lt_val] using ha')
    change _ = (‚ü®‚ü®b, hb‚ü©, ‚ü®a' + 1, ha'‚ü©‚ü© : (Œ£‚Çó (b : Fin (n + 1)), Fin b.succ)) at eq
    convert W.comp_mem _ _ one two
    exact eq.symm
    rw [‚Üê F.map_comp, CategoryTheory.homOfLE_comp]
    congr!
    exact eq.symm
  ¬∑ next h'' =>
    simp at h''
    subst h''
    simp only [Fin.val_succ, Fin.succ_mk, CategoryTheory.homOfLE_refl,
      CategoryTheory.Functor.map_id]
    apply id_mem

lemma _root_.CategoryTheory.MorphismProperty.map_mem_of_sigma {n : ‚Ñï} (F : (Œ£‚Çó (b : Fin (n + 1)), Fin b.succ) ‚•§ C)
    (hF : ‚àÄ (i : Œ£‚Çó (b : Fin (n + 1)), Fin b.succ), W (F.map (homOfLE (Sigma.Lex.le_succ i))))
    {i j : Œ£‚Çó (b : Fin (n + 1)), Fin b.succ} (f : i ‚ü∂ j) :
    W (F.map f) := by
  have h : i ‚â§ j := leOfHom f
  obtain ‚ü®‚ü®b, hb‚ü©, ‚ü®a, ha‚ü©‚ü© := i
  obtain ‚ü®‚ü®b', hb'‚ü©, ‚ü®a', ha'‚ü©‚ü© := j
  have hbb' : b ‚â§ b' := by
    cases h
    ¬∑ next h => exact h.le
    ¬∑ next => exact le_rfl
  obtain ‚ü®k, hk‚ü© := Nat.le.dest hbb'
  induction k with
  | zero =>
    subst hk
    change W (F.map (homOfLE _))
    apply W.map_mem_of_sigma' F hF
    simpa [Sigma.Lex.le_def] using h
  | succ k hk =>
    cases (lt_or_eq_of_le hbb')
    ¬∑ next hbb' =>
      have ba_bb : (toLex ‚ü®‚ü®b, hb‚ü©, ‚ü®a, ha‚ü©‚ü© : Œ£‚Çó (b : Fin (n + 1)), Fin b.succ) ‚â§ toLex ‚ü®‚ü®b, hb‚ü©, ‚ü®b, Nat.lt_add_one b‚ü©‚ü© := by
        simp at ha ‚ä¢
        right
        simp
        omega
      have b'0_b'a' : (toLex ‚ü®‚ü®b', hb'‚ü©, ‚ü®0, Nat.zero_lt_succ b'‚ü©‚ü© : Œ£‚Çó (b : Fin (n + 1)), Fin b.succ) ‚â§ toLex ‚ü®‚ü®b', hb'‚ü©, ‚ü®a', ha'‚ü©‚ü© := by
        right
        simp
      have bb_b'0 : (toLex ‚ü®‚ü®b, hb‚ü©, ‚ü®b, Nat.lt_add_one b‚ü©‚ü© : Œ£‚Çó (b : Fin (n + 1)), Fin b.succ) ‚â§ toLex ‚ü®‚ü®b', hb'‚ü©, ‚ü®0, Nat.zero_lt_succ b'‚ü©‚ü© := by
        left
        simpa
      suffices W (F.map (homOfLE bb_b'0)) by
        have := (W.comp_mem (F.map <| homOfLE ba_bb) (F.map (homOfLE bb_b'0)) ?_ this)
        rw [‚Üê F.map_comp, CategoryTheory.homOfLE_comp] at this
        have := W.comp_mem _ (F.map <| homOfLE b'0_b'a') (this) ?_
        rw [‚Üê F.map_comp, CategoryTheory.homOfLE_comp] at this
        exact this
        ¬∑ apply W.map_mem_of_sigma' F hF
          simp
        ¬∑ apply W.map_mem_of_sigma' F hF
          simp at ha ‚ä¢
          omega
      subst hk
      let P (k : ‚Ñï) := ‚àÄ (b : ‚Ñï) (hk : 0 < k) (hbk : b + k < n + 1),
        W (F.map (homOfLE (show toLex ‚ü®‚ü®b, Nat.lt_of_add_right_lt hbk‚ü©, ‚ü®b, Nat.lt_add_one _‚ü©‚ü© ‚â§ toLex ‚ü®‚ü®b + k, hbk‚ü©, ‚ü®0, Nat.zero_lt_succ _‚ü©‚ü© by left; simpa)))
      suffices ‚àÄ (k : ‚Ñï), P k by
        exact this (k + 1) b (Nat.zero_lt_succ _) (by omega)
      intro k b hk hbk
      induction k with
      | zero => omega
      | succ k hk =>
        induction k with
        | zero =>
          have goal := hF (toLex ‚ü®‚ü®b, Nat.lt_of_add_right_lt hbk‚ü©, ‚ü®b, Nat.lt_add_one _‚ü©‚ü©)
          convert (config := .unfoldSameFun) goal
          all_goals exact (Sigma.Lex.Fin.succ_eq_of_lt_last ‚ü®b, Nat.lt_of_add_right_lt hbk‚ü© (by simp [Fin.lt_iff_val_lt_val]; omega)).symm
        | succ k hk' =>
          rename_i hk''
          have goal := hk'' (by omega) (by omega)
          have := @homOfLE_comp (Œ£‚Çó (b : Fin (n + 1)), Fin ‚Üëb.succ) _ ‚ü®‚ü®b, Nat.lt_of_add_right_lt hbk‚ü©, ‚ü®b, Nat.lt_add_one _‚ü©‚ü© ‚ü®‚ü®b + k + 1, by omega‚ü©, ‚ü®0, by simp‚ü©‚ü© ‚ü®‚ü®b + (k + 1 + 1), hbk‚ü©, ‚ü®0, by simp‚ü©‚ü© (by left; simp; omega) (by left; simp; omega)
          rw [‚Üê this, F.map_comp]
          apply comp_mem
          ¬∑ exact goal
          ¬∑ have := @homOfLE_comp (Œ£‚Çó (b : Fin (n + 1)), Fin ‚Üëb.succ) _ ‚ü®‚ü®b + k + 1, by omega‚ü©, ‚ü®0, by simp‚ü©‚ü© ‚ü®‚ü®b + k + 1, by omega‚ü©, ‚ü®b + k + 1, by simp‚ü©‚ü© ‚ü®‚ü®b + (k + 1 + 1), hbk‚ü©, ‚ü®0, by simp‚ü©‚ü© (by right; simp) (by left; simp; omega)
            rw [‚Üê this, F.map_comp]
            apply comp_mem
            ¬∑ apply W.map_mem_of_sigma' F hF
              simp
            ¬∑ have goal := hF ‚ü®‚ü®b + k + 1, by omega‚ü©, ‚ü®b + k + 1, by simp‚ü©‚ü©
              convert (config := .unfoldSameFun) goal
              all_goals exact (Sigma.Lex.Fin.succ_eq_of_lt_last ‚ü®b + k + 1, by omega‚ü© (by simp [Fin.lt_iff_val_lt_val]; omega)).symm
    ¬∑ next hbb' =>
      subst hbb'
      apply W.map_mem_of_sigma' F hF
      omega

end

lemma œÑ.filtration_last_innerAnodyne : innerAnodyne
    (homOfLE (filtration_monotone (Sigma.Lex.le_succ ‚ü®Fin.last (n + 1), Fin.last (n + 1)‚ü©))) := by
  rw [innerAnodyne_eq_saturation_innerHornInclusions]
  refine (arrow_mk_iso_iff _ ?_).2 <| id_mem innerHornInclusions.saturation (filtration ‚ü®Fin.last (n + 1), Fin.last (n + 1)‚ü©).toSSet
  exact Arrow.isoMk (isoOfEq rfl) (isoOfEq (congrArg filtration Sigma.Lex.Fin.succ_last_eq_last))

lemma œÉ.filtration_last_innerAnodyne : innerAnodyne
    (homOfLE (filtration_monotone (Sigma.Lex.le_succ ‚ü®Fin.last n, Fin.last n‚ü©))) := by
  rw [innerAnodyne_eq_saturation_innerHornInclusions]
  refine (arrow_mk_iso_iff _ ?_).2 <| id_mem innerHornInclusions.saturation (filtration ‚ü®Fin.last n, Fin.last n‚ü©).toSSet
  exact Arrow.isoMk (isoOfEq rfl) (isoOfEq (congrArg filtration Sigma.Lex.Fin.succ_last_eq_last))

lemma œÉ.succ_innerAnodyne (i : Œ£‚Çó (b : Fin (n + 1)), Fin b.succ) :
    innerAnodyne (homOfLE (filtration_monotone (Order.le_succ i))) := by
  obtain hn | rfl := lt_or_eq_of_le (OrderTop.le_top i)
  ¬∑ rw [innerAnodyne_eq_saturation_innerHornInclusions]
    have œÉsq := œÉ.filtrationPushout_intermediate i hn
    rw [œÉ.innerHornImage, œÉ.subcomplex, ofSimplex_eq_range, œÉ.s] at œÉsq
    refine of_isPushout (Sq.isPushout œÉsq).flip
      ((arrow_mk_iso_iff _ (image_arrow_iso_of_mono _ _)).2
        (.of _ (.mk (Nat.lt_of_sub_eq_succ rfl) (?_))))
    ¬∑ obtain ‚ü®b, a‚ü© := i
      rw [Fin.lt_iff_val_lt_val]
      simp only [Fin.val_succ, Fin.succ_mk, Fin.zero_eta, Fin.castSucc_mk,
        Fin.val_last, add_lt_add_iff_right]
      omega
  ¬∑ exact filtration_last_innerAnodyne

lemma œÉ.filtration_innerAnodyne {i j : Œ£‚Çó (b : Fin (n + 1)), Fin b.succ} (h : i ‚â§ j) :
    innerAnodyne (homOfLE (filtration_monotone h)) :=
  innerAnodyne.map_mem_of_sigma
    (filtration_monotone.functor ‚ãô forget _) œÉ.succ_innerAnodyne (homOfLE h)

lemma œÑ.succ_innerAnodyne (i : Œ£‚Çó (b : Fin (n + 2)), Fin b.succ) :
    innerAnodyne (homOfLE (filtration_monotone (Order.le_succ i))) := by
  obtain hn | rfl := lt_or_eq_of_le (OrderTop.le_top i)
  ¬∑ rw [innerAnodyne_eq_saturation_innerHornInclusions]
    have œÑsq := œÑ.filtrationPushout_intermediate i hn
    rw [œÑ.innerHornImage, œÑ.subcomplex, ofSimplex_eq_range, œÑ.t] at œÑsq
    refine of_isPushout (Sq.isPushout œÑsq).flip
      ((arrow_mk_iso_iff _ (image_arrow_iso_of_mono _ _)).2
        (.of _ (.mk (Nat.lt_of_sub_eq_succ rfl) (?_))))
    ¬∑ obtain ‚ü®b, a‚ü© := i
      rw [Fin.lt_iff_val_lt_val]
      simp only [Fin.val_succ, Fin.succ_mk, Fin.zero_eta, Fin.castSucc_mk,
        Fin.val_last, add_lt_add_iff_right]
      omega
  ¬∑ exact filtration_last_innerAnodyne

lemma œÑ.filtration_innerAnodyne {i j : Œ£‚Çó (b : Fin (n + 2)), Fin b.succ} (h : i ‚â§ j) :
    innerAnodyne (homOfLE (filtration_monotone h)) :=
  innerAnodyne.map_mem_of_sigma
    (filtration_monotone.functor ‚ãô forget _) œÑ.succ_innerAnodyne (homOfLE h)

lemma œÉ.bot_innerAnodyne : innerAnodyne (homOfLE (œÉ.filtrationPushout_zero (n := n)).le‚ÇÉ‚ÇÑ) := by
  rw [innerAnodyne_eq_saturation_innerHornInclusions]
  let œÉsq := (œÉ.filtrationPushout_zero (n := n))
  rw [Sigma.Lex.bot_eq_zero, œÉ.subcomplex, ofSimplex_eq_range] at œÉsq
  refine of_isPushout œÉsq.isPushout.flip
        ((arrow_mk_iso_iff _ (image_arrow_iso_of_mono (œÉ.s ‚ä•) Œõ[n + 2, 1])).2
          (.of _ (.mk Fin.zero_lt_one Fin.one_lt_last)))

lemma œÑ.bot_innerAnodyne : innerAnodyne (homOfLE (œÑ.filtrationPushout_zero (n := n)).le‚ÇÉ‚ÇÑ) := by
  rw [innerAnodyne_eq_saturation_innerHornInclusions]
  let œÑsq := (œÑ.filtrationPushout_zero (n := n))
  rw [Sigma.Lex.bot_eq_zero, œÑ.subcomplex, ofSimplex_eq_range] at œÑsq
  refine of_isPushout œÑsq.isPushout.flip
        ((arrow_mk_iso_iff _ (image_arrow_iso_of_mono (œÑ.t ‚ä•) Œõ[n + 3, 1])).2
          (.of _ (.mk Fin.zero_lt_one Fin.one_lt_last)))

lemma unionProd_Œπ_innerAnodyne : innerAnodyne (‚àÇŒî[n].unionProd Œõ[2, 1]).Œπ := by
  induction n with
  | zero =>
    rw [boundary_zero, innerAnodyne_eq_saturation_innerHornInclusions]
    exact (arrow_mk_iso_iff _ zero_unionProd_arrowIso).2
      (.of _ (.mk Fin.zero_lt_one Fin.one_lt_last))
  | succ n _ =>
    change innerAnodyne
        ((homOfLE œÉ.filtrationPushout_zero.le‚ÇÉ‚ÇÑ) ‚â´
        (homOfLE (œÉ.filtration_monotone bot_le)) ‚â´
        (homOfLE œÑ.filtrationPushout_zero.le‚ÇÉ‚ÇÑ) ‚â´
        (homOfLE (œÑ.filtration_monotone bot_le)) ‚â´
        (isoOfEq œÑ.filtration_last).hom ‚â´
        (topIso _).hom)
    refine comp_mem _ _ _ œÉ.bot_innerAnodyne <|
      comp_mem _ _ _ (œÉ.filtration_innerAnodyne bot_le) <|
      comp_mem _ _ _ œÑ.bot_innerAnodyne <|
      comp_mem _ _ _ (œÑ.filtration_innerAnodyne bot_le) <|
      comp_mem _ _ _ (of_isIso _ _) (of_isIso _ _)

noncomputable
def arrow_unionProd_iso : Arrow.mk (Œõ[2, 1].Œπ ‚ñ° ‚àÇŒî[n].Œπ) ‚âÖ Arrow.mk (‚àÇŒî[n].unionProd Œõ[2, 1]).Œπ := by
  refine Arrow.isoMk
    ((IsPushout.isoPushout (unionProd.isPushout _ _)).symm ‚â™‚â´ unionProd.symmIso _ _) (Œ≤_ Œî[2] Œî[n]) ?_
  apply Limits.pushout.hom_ext
  all_goals
  ¬∑ simp [Functor.PushoutObjObj.Œπ]
    aesop

lemma hornBoundaryPushout_innerAnodyne : innerAnodyne (Œõ[2, 1].Œπ ‚ñ° ‚àÇŒî[n].Œπ) :=
  (arrow_mk_iso_iff _ arrow_unionProd_iso).2 unionProd_Œπ_innerAnodyne

inductive HornBoundaryPushout : {X Y : SSet} ‚Üí (X ‚ü∂ Y) ‚Üí Prop
  | mk (m : ‚Ñï) : HornBoundaryPushout (Œõ[2, 1].Œπ ‚ñ° ‚àÇŒî[m].Œπ)

/-- the class of pushout-products of `‚àÇŒî[m] ‚Ü™ Œî[m]` with `Œõ[2, 1] ‚Ü™ Œî[2]`. -/
def hornBoundaryPushouts : MorphismProperty SSet := fun _ _ p ‚Ü¶ HornBoundaryPushout p

/-- `boundaryInclusions` is contained in the class of all morphisms `i : A ‚Üí B` such that
the pushout-product with `Œõ[2, 1] ‚Ü™ Œî[2]` is in the saturation of `hornBoundaryPushouts`. -/
lemma boundaryInclusions_le_S : boundaryInclusions ‚â§
  (saturation.{w} hornBoundaryPushouts).pushoutProduct Œõ[2, 1].Œπ := fun _ _ _ ‚ü®_‚ü© ‚Ü¶ .of _ (.mk _)

lemma monomorphisms_le_S : monomorphisms SSet.{w} ‚â§
    (saturation.{w} hornBoundaryPushouts).pushoutProduct Œõ[2, 1].Œπ := by
  rw [monomorphisms_eq_saturation_boundaryInclusions, ‚Üê Saturated.le_iff]
  exact boundaryInclusions_le_S

lemma innerAnodyne_le_saturation_hornBoundaryPushouts :
    innerAnodyne.{w} ‚â§ (saturation.{w} hornBoundaryPushouts) := by
  rw [innerAnodyne_eq_saturation_innerHornInclusions, ‚Üê Saturated.le_iff]
  intro _ _ _ ‚ü®h0, hn‚ü©
  exact .retract (hornRetract _ h0 hn) (monomorphisms_le_S _ (.infer_property _))

lemma saturation_hornBoundaryPushouts_le_innerAnodyne :
    (saturation.{w} hornBoundaryPushouts) ‚â§ innerAnodyne := by
  rw [‚Üê Saturated.le_iff]
  intro _ _ _ ‚ü®_‚ü©
  exact hornBoundaryPushout_innerAnodyne

-- `007F` (b)
lemma innerAnodyne_eq_saturation_hornBoundaryPushouts : innerAnodyne.{w} = (saturation.{w} hornBoundaryPushouts) :=
  le_antisymm (innerAnodyne_le_saturation_hornBoundaryPushouts)
    (saturation_hornBoundaryPushouts_le_innerAnodyne)

-- `007F` (a)
lemma hornMonoPushout_innerAnodyne {A B : SSet} (i : A ‚ü∂ B) [Mono i] :
    innerAnodyne (Œõ[2, 1].Œπ ‚ñ° i) := by
  rw [innerAnodyne_eq_saturation_hornBoundaryPushouts]
  exact monomorphisms_le_S i (.infer_property _)
